buildscript {
    ext {
        kotlinVersion = properties["kotlin.version"]
        springBootVersion = properties["spring.boot.version"]
        junitVersion = properties["junit.version"]
        junitPlatformVersion = properties["junit.platform.version"]
        spekVersion = properties["spek.version"]
        mockkVersion = properties["mockk.version"]
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
    }
}

apply {
    plugin "kotlin" // kotlin-gradle-plugin: compile kotlin code
    plugin "kotlin-jpa" // kotlin-noarg: no-arg constructor
    plugin "kotlin-spring" // kotlin-allopen: opens classes for spring
    plugin "org.springframework.boot" // spring-boot-gradle-plugin
    plugin "io.spring.dependency-management" // spring-boot-gradle-plugin (transitive): not automatically applied
    // automatically import the spring-boot-dependencies bom from the version of Spring Boot currently used
}

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bootJar.mainClassName = "org.dailyepisode.ApplicationKt"

test {
    useJUnitPlatform {
        includeEngines "spek"
    }
}

repositories {
    mavenCentral()
    maven { setUrl("https://repo.spring.io/milestone") }
    maven { setUrl("https://repo.spring.io/snapshot") }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile project(':infrastructure')
    compile project(':domain')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin"

    testCompile "org.springframework.boot:spring-boot-starter-test"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.junit.platform:junit-platform-engine:$junitPlatformVersion"

    testCompile "org.jetbrains.spek:spek-api:$spekVersion"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
}
